functor(Tuple, Functor)
functor(Cons, Functor)
functor(Token, Functor)
functor(Info, Functor) //Info(line,column)

rel digit(c)
	string.lessOrEqual(c,'9')
	string.lessOrEqual('0',c)

rel letter(c)
	string.lessOrEqual(c,'z') and string.lessOrEqual('a',c) or
	string.lessOrEqual(c,'Z') and string.lessOrEqual('A',c)

rel single_quote(c) 
	string.code(c, 39)
rel slash(c) 
	string.code(c, 92)
rel double_quote(c) 
	c='\"'
rel quote(c) 
	this.single_quote(c) or this.double_quote(c)
rel endline(c)
	c='\n'
rel whitespace(c) 
	c=' ' or c='\t' or c='\n'

rel match_rel2(s,i,i2,f)
	string.at(s,i,c)
	f(c) and this.match_rel2(s,i+1,i2,f) or
	i=i2

rel match_some(s,i,i2,f)
	string.at(s,i,c)
	f(c) and this.match_rel2(s,i+1,i2,f)

rel match_one(s,i,i2,f)
	i2=i+1
	string.at(s,i,c)
	f(c)

rel match_any(s,i,i2,f)
	string.at(s,i,c)
	f(c) and this.match_any(s,i+1,i2,f) or
	i=i2

rel match_until(s,i,i2,f)
	string.at(s,i,c)
	f(c) and i=i2 or
	this.match_until(s,i+1,i2,f)

rel match_string(s,i,i2,str)
	string.size(str,size)
	i2=i+size
	string.slice(s,i,i2,str)

rel navigate_until_asterisk(s,i,i2,info,info2)
	info=Info(line,col)
	string.at(s,i,c)
	if(c='*')
		i=i2 and info2=info
	else
		if(c='\n')
			info1=Info(line+1, 1)
		elseif(c='\t')
			info1=Info(line, col+4)
		else
			info1=Info(line, col+1)
		this.navigate_until_asterisk(s,i+1,i2,info1,info2)
	
//parses comment and updates line/column information
rel navigate_comment(s,i,i2,info,info2)
	if(this.match_string(s,i,x,'//'))
		info2=info
		if(string.findIndex(s,'\n',x,i2))
			true
		else
			i2=string.size(s)
	else
		this.match_string(s,i,x,'/*')
		this.navigate_until_asterisk(s,x,y,info,info2)
		this.match_string(s,y,i2,'*/')

//parses whitespace and updates line/column information
rel navigate_ws2(String s,Integer i,Integer i2,line,col,info2)
	if(string.at(s,i,c))
		if(c='\n')
			navigate_ws2(s,i+1,i2,line+1,1,info2)
		elseif(c='\t')
			navigate_ws2(s,i+1,i2,line,col+4,info2)
		elseif(c='\r')
			navigate_ws2(s,i+1,i2,line,col,info2)
		else
			this.whitespace(c)
			navigate_ws2(s,i+1,i2,line,col+1,info2)
	else
		i2=i
		info2=Info(line,col)
	
rel navigate_whitespace(s,i,i2,info,info2)
	string.at(s,i,c)
	this.whitespace(c)
	info=Info(line,col)
	if(c='\n')
		navigate_ws2(s,i+1,i2,line+1,1,info2)
	else
		navigate_ws2(s,i+1,i2,line,col,info2)
		
//matching relations

rel match_whitespace(s,i,i2)
	match_some(s,i,i2,this.whitespace)

rel parse_double_string(s,i,i2)
	string.at(s,i,c)
	if(double_quote(c))
		i=i2
	elseif(slash(c))
		parse_double_string(s,i+2,i2)
	else
		parse_double_string(s,i+1,i2)
		
rel parse_string(s,i,i2)
	j=i+1
	string.at(s,i,c)
	if(double_quote(c))
		parse_double_string(s,j,j2)
		i2=j2+1
	else
		single_quote(c)
		match_until(s,j,j2,this.single_quote)
		i2=j2+1

rel parse_number(s,i,i2)
	match_some(s,i,x1,this.digit)
	match_string(s,x1,x2,'.')
	match_some(s,x2,i2,this.digit) or
	
	match_some(s,i,i2,this.digit)

rel parse_id(s,i,i2)
	rel p(c) letter(c) or c='_';
	match_some(s,i,x,p)
	match_any(s,x,i2,this.digit)
	
//tries to find if word c is in list, if so set c=z
rel match_list(l,s,z)
	l=Cons(head, tail)
	choose(head=s)
		z=s
	else
		match_list(tail, s, z)

rel step(s, Integer i, Integer j, z, info, info2)
	if(this.navigate_whitespace(s,i,j,info,info2))
		z='whitespace'
	elseif(this.navigate_comment(s,i,j,info,info2))
		z='comment'
	else
		string.at(s,i,c)
		k=i+1
		if(this.match_list(['=',';','(',')','{','}','.',','], c, z))
			j=i+1
		elseif(this.letter(c))
			this.match_string(s,i,j,'rel') and z='rel' or
			this.match_string(s,i,j,'and') and z='connective' or
			this.match_string(s,i,j,'or') and z='connective' or
			this.match_string(s,i,j,'true') and z='true' or
			this.match_string(s,i,j,'false') and z='false' or
			this.match_string(s,i,j,'if') and z='if' or
			this.match_string(s,i,j,'choose') and z='choose' or
			this.match_string(s,i,j,'elseif') and z='elseif' or
			this.match_string(s,i,j,'else') and z='else' or
			this.match_string(s,i,j,'once') and z='once'
		else
			this.match_string(s,i,j,'!=') and z='!=' or
			this.match_string(s,i,j,'::') and z='::' or
			this.parse_id(s,i,j) and z='id' or
			this.parse_number(s,i,j) and z='number' or
			this.parse_string(s,i,j) and z='string' or
			this.match_string(s,i,j,'<=') and z='<=' or
			this.match_string(s,i,j,'>=') and z='>=' or
			this.match_list(['+','-','<','>','[',']','/','*'], c, z) and j=k or
			this.match_one(s,i,j,this.single_quote) and z='single_quote' or
			this.match_one(s,i,j,this.double_quote) and z='double_quote' or
			info=Info(line,col)
			logic.throw('(line '+math.integerToString(line)+', col '+math.integerToString(col)+') cannot understand character')
		info=Info(Integer line, Integer col)
		info2=Info(line, col+j-i)
		
rel run2(s,start,l,info,info2)
	if(true)
		this.step(s,start,x,type,info,info1)
		this.run2(s,x,l0,info1,info2)
		if(type!='comment')
			string.slice(s,start,x,lexeme)
			tk=Token(lexeme, type,info)
			list.push(l0,tk,l)
		else
			l=l0
	else
		l=Cons(Token('EOF','EOF',_), Cons)
		string.size(s,z)
		z=start
		info2=info
	
rel run(s,start,l)
	this.run2(s,start,l,Info(1,1),info2)
